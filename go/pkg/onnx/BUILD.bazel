load("@rules_go//go:def.bzl", "go_library", "go_test")

# Copy ONNX model files from data/ for go:embed.
genrule(
    name = "embed_models",
    srcs = [
        "//data/models/onnx:speaker_eres2net",
        "//data/models/onnx:vad_silero",
        "//data/models/onnx:denoise_nsnet2",
    ],
    outs = [
        "speaker_eres2net.onnx",
        "vad_silero.onnx",
        "denoise_nsnet2.onnx",
    ],
    cmd = """
        for f in $(SRCS); do
            cp $$f $(RULEDIR)/$$(basename $$f)
        done
    """,
)

# Bazel places shared libraries in _solib_{platform}/{mangled}/ under the
# bin directory. Go test binaries are at bin/{pkg}/{test}_/{test}.
# We add rpath entries to search relative paths from the binary to _solib.
_SOLIB_SUBDIR = "_U_A_A+onnxruntime_Uext+onnxruntime_S_S_Connxruntime_Ulib___Ulib"

go_library(
    name = "onnx",
    srcs = [
        "model.go",
        "model_embed.go",
        "onnx.go",
    ],
    cgo = True,
    embedsrcs = [":embed_models"],
    importpath = "github.com/haivivi/giztoy/go/pkg/onnx",
    visibility = ["//visibility:public"],
    cdeps = ["@onnxruntime"],
    clinkopts = select({
        "@platforms//os:macos": [
            # Direct (for native go build / manual deployment)
            "-Wl,-rpath,@loader_path",
            # Bazel bin layout: binary at bin/{pkg}/{test}_/test -> solib at bin/_solib_*/
            "-Wl,-rpath,@loader_path/../../../../_solib_darwin_arm64/" + _SOLIB_SUBDIR,
            "-Wl,-rpath,@loader_path/../../../../../_solib_darwin_arm64/" + _SOLIB_SUBDIR,
            "-Wl,-rpath,@loader_path/../../../../../../_solib_darwin_arm64/" + _SOLIB_SUBDIR,
        ],
        "@platforms//os:linux": [
            "-Wl,-rpath,$$ORIGIN",
            "-Wl,-rpath,$$ORIGIN/../../../../_solib_k8/" + _SOLIB_SUBDIR,
            "-Wl,-rpath,$$ORIGIN/../../../../../_solib_k8/" + _SOLIB_SUBDIR,
            "-Wl,-rpath,$$ORIGIN/../../../../../../_solib_k8/" + _SOLIB_SUBDIR,
        ],
        "//conditions:default": [],
    }),
)

go_test(
    name = "onnx_test",
    srcs = [
        "compare_test.go",
        "onnx_test.go",
    ],
    embed = [":onnx"],
    deps = [
        "//go/pkg/ncnn",
    ],
)
