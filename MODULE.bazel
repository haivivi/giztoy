"""Bazel module definition for giztoy."""

module(
    name = "giztoy",
    version = "0.0.1",
)

# Skylib (required by gazelle)
bazel_dep(name = "bazel_skylib", version = "1.9.0")

# Go rules
bazel_dep(name = "rules_go", version = "0.59.0")
bazel_dep(name = "gazelle", version = "0.47.0")

# Platform rules for platform detection
bazel_dep(name = "platforms", version = "1.0.0")

# Rust rules
bazel_dep(name = "rules_rust", version = "0.68.1")

# C/C++ rules
bazel_dep(name = "rules_cc", version = "0.1.1")

# Shell rules (required for sh_binary/sh_test in Bazel 8+)
bazel_dep(name = "rules_shell", version = "0.6.1")

# Go SDK
go_sdk = use_extension("@rules_go//go:extensions.bzl", "go_sdk")
go_sdk.download(version = "1.25.5")

# Go dependencies from go.mod
go_deps = use_extension("@gazelle//:extensions.bzl", "go_deps")
go_deps.from_file(go_mod = "//go:go.mod")
use_repo(
    go_deps,
    "com_github_eclipse_paho_golang",
    "com_github_goccy_go_yaml",
    "com_github_google_jsonschema_go",
    "com_github_google_uuid",
    "com_github_googleapis_gax_go_v2",
    "com_github_gorilla_websocket",
    "com_github_kaptinlin_jsonrepair",
    "com_github_mochi_mqtt_server_v2",
    "com_github_openai_openai_go",
    "com_github_spf13_cobra",
    "org_golang_google_genai",
)

# yq tool for YAML processing
yq = use_extension("//:extensions.bzl", "yq")
use_repo(yq, "yq")

# Rust toolchain
rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(edition = "2024")
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

# Rust crate dependencies from Cargo.toml (crate_universe)
crate = use_extension("@rules_rust//crate_universe:extension.bzl", "crate")
crate.from_cargo(
    name = "crate_index",
    cargo_lockfile = "//rust:Cargo.lock",
    manifests = [
        "//rust:Cargo.toml",
        "//rust/audio:Cargo.toml",
        "//rust/buffer:Cargo.toml",
        "//rust/genx:Cargo.toml",
        "//rust/dashscope:Cargo.toml",
        "//rust/minimax:Cargo.toml",
        "//rust/mqtt0:Cargo.toml",
        "//rust/cli:Cargo.toml",
        "//rust/cmd/dashscope:Cargo.toml",
        "//rust/cmd/minimax:Cargo.toml",
        "//examples/rust/audio:Cargo.toml",
        "//examples/rust/minimax:Cargo.toml",
        "//examples/rust/genx:Cargo.toml",
    ],
)
use_repo(crate, "crate_index")

# Audio libraries (soxr, portaudio, opus, lame, ogg)
audio_libs = use_extension("//:extensions.bzl", "audio_libs")
use_repo(audio_libs, "lame", "ogg", "opus", "portaudio", "soxr")
