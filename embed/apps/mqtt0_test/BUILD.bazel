# mqtt0 Test App
#
# Tests MQTT 5.0 client with Topic Alias support on ESP32-S3 boards.
# Includes comprehensive test suites for:
# - Basic connectivity
# - Multi-topic concurrent publish
# - Throughput measurement (up/down)
# - Long connection stability
# - Reconnection handling
#
# Usage:
#   bazel build //embed/apps/mqtt0_test:esp \
#     --define WIFI_SSID=HAIVIVI-MFG \
#     --define WIFI_PASSWORD=xxx \
#     --define MQTT_HOST=mqtt.haivivi.cn \
#     --define MQTT_PORT=8883 \
#     --define TEST_SUITE=all

load("@embed_zig//bazel/esp:defs.bzl", "esp_flash", "esp_sdkconfig", "esp_zig_app")
load("@embed_zig//bazel:env.bzl", "make_env_file")
load("@embed_zig//bazel/esp/sdkconfig:app.bzl", "esp_app")
load("@embed_zig//bazel/esp/sdkconfig:crypto.bzl", "esp_crypto")
load("@embed_zig//bazel/esp/sdkconfig:lwip.bzl", "esp_lwip")
load("@embed_zig//bazel/esp/sdkconfig:newlib.bzl", "esp_newlib")
load("@embed_zig//bazel/esp/sdkconfig:psram.bzl", "esp_psram")
load("@embed_zig//bazel/esp/sdkconfig:wifi.bzl", "esp_wifi")

package(default_visibility = ["//visibility:public"])

filegroup(
    name = "srcs",
    srcs = glob(["**/*"]),
)

# Environment from command line --define flags
make_env_file(
    name = "env",
    defines = [
        "WIFI_SSID",
        "WIFI_PASSWORD",
        "MQTT_HOST",
        "MQTT_PORT",
        "MQTT_USERNAME",
        "MQTT_PASSWORD",
        "CLIENT_ID",
        "TEST_SUITE",
        "MQTT_SKIP_VERIFY",
    ],
    defaults = {
        "WIFI_SSID": "HAIVIVI-MFG",
        "WIFI_PASSWORD": "!haivivi",
        "MQTT_HOST": "mqtt.stage.haivivi.cn",
        "MQTT_PORT": "8883",
        "MQTT_USERNAME": "admin",
        "MQTT_PASSWORD": "isA953Nx56EBfEu",
        "CLIENT_ID": "zig-mqtt0-test",
        "TEST_SUITE": "remote_bandwidth",
        "MQTT_SKIP_VERIFY": "true",
    },
)

# =============================================================================
# App-specific sdkconfig components
# =============================================================================

# XIP PSRAM - allows PSRAM access during WiFi/flash operations
esp_psram(
    name = "psram_xip",
    chip = "esp32s3",
    mode = "oct",
    speed = "80m",
    xip_from_psram = True,
)

# 128KB PSRAM stack for TLS/MQTT operations
esp_app(
    name = "app_128k",
    run_in_psram = 131072,  # 128KB
)

# mbedTLS with hardware-accelerated crypto
esp_crypto(
    name = "crypto_mbedtls",
    disable_mbedtls = False,
)

# Newlib nano printf - saves ~20KB
esp_newlib(
    name = "newlib_nano",
    nano_format = True,
)

# High throughput WiFi configuration
esp_wifi(
    name = "wifi_fast",
    static_rx_buffer_num = 24,      # More static buffers (default: 16)
    dynamic_rx_buffer_num = 128,    # Double dynamic buffers (default: 64)
    rx_ba_win = 32,
    tx_ba_win = 16,                 # Larger TX window (default: 6)
)

# High throughput LWIP configuration
esp_lwip(
    name = "lwip_fast",
    tcpip_task_stack_size = 8192,
    max_sockets = 10,
    max_active_tcp = 16,
    tcp_snd_buf_default = 65535,    # Max TCP send buffer
    tcp_wnd_default = 65535,        # Max TCP window
    tcp_recvmbox_size = 64,
)

# Combined sdkconfig for this app (high throughput)
esp_sdkconfig(
    name = "sdkconfig",
    core = "//embed/esp:core",
    crypto = ":crypto_mbedtls",
    freertos = "//embed/esp:freertos",
    log = "//embed/esp:log",
    lwip = ":lwip_fast",            # High throughput LWIP
    newlib = ":newlib_nano",
    psram = ":psram_xip",
    wifi = ":wifi_fast",            # High throughput WiFi
)

# =============================================================================
# App build
# =============================================================================

esp_zig_app(
    name = "esp",
    app = ":srcs",
    app_config = ":app_128k",
    boards = ["korvo2_v3", "esp32s3_devkit"],
    env = ":env",
    extra_cmake = [
        "include(${_ESP_LIB}/esp/src/sal/i2c.cmake)",
        "include(${_ESP_LIB}/esp/idf/src/event/event.cmake)",
        "include(${_ESP_LIB}/esp/idf/src/wifi/wifi.cmake)",
        "include(${_ESP_LIB}/esp/idf/src/mbed_tls/mbed_tls.cmake)",
        "include(${_ESP_LIB}/esp/idf/src/net/net.cmake)",
    ],
    extra_c_sources = [
        "I2C_C_SOURCES",
        "EVENT_SRCS",
        "WIFI_C_SOURCES",
        "MBED_TLS_C_SOURCES",
        "NET_C_SOURCES",
    ],
    deps = [
        "@embed_zig//lib/hal",
        "@embed_zig//lib/trait",
        "@embed_zig//lib/drivers",
        "@embed_zig//lib/dns",
        "@embed_zig//lib/tls",
    ],
    force_link = [
        "led_strip_refresh",
        "led_strip_new_rmt_device",
        "${I2C_FORCE_LINK}",
        "${EVENT_FORCE_LINK}",
        "${WIFI_FORCE_LINK}",
        "${NET_FORCE_LINK}",
        "verify_with_esp_bundle",
    ],
    idf_deps = [
        "espressif/led_strip:^3.0.0",
    ],
    requires = [
        "driver",
        "led_strip",
        "esp_driver_i2c",
        "esp_wifi",
        "esp_event",
        "esp_netif",
        "lwip",
        "mbedtls",
        "nvs_flash",
    ],
    sdkconfig = ":sdkconfig",
    tags = ["manual"],  # Skip in CI - requires ESP-IDF
)

esp_flash(
    name = "flash",
    app = ":esp",
    tags = ["manual"],  # Skip in CI - requires ESP-IDF
)
